import java.util.*;

COMPILER Compiler

public SymbolTable st;
public CodeGenerator gen;

private Printer printer;
private int MAIN_LABEL = -1;

private static final int // types
        UNDEFINED=0, INTEGER=1, BOOLEAN=2, INTEGER_ARRAY=3;

public static final Map<Integer, String> TYPES_INVERSE;
static {
  Map<Integer, String> m = new HashMap<Integer, String>();
  m.put(0, "undefined");
  m.put(1, "int");
  m.put(2, "boolean");
  m.put(3, "int[]");
  TYPES_INVERSE = Collections.unmodifiableMap(m);
}

/*----------------------------------------------------------------------------*
 * Helper functions:                                                          *
 * Raises an error in case of incompatible types                              *
 *----------------------------------------------------------------------------*/

private void checkBoolean(int type, String operation) {
  if (type != BOOLEAN) SemErr("type error: " +
                                 operation +
                                 " expects boolean type, got " +
                                 TYPES_INVERSE.get(type));
}

private void checkInt(int type, String operation) {
  if (type != INTEGER) SemErr("type error: " +
                                 operation +
                                 " expects integer type, got " +
                                 TYPES_INVERSE.get(type));
}

private void checkArrayType(int type, String operation) {
  if (type != INTEGER_ARRAY) SemErr("type error: " +
                                       operation +
                                       "expects integer array type, got " +
                                       TYPES_INVERSE.get(type));
}

private void checkCallParameterTypes(SymbolTable.Function fn, ArrayList<Integer> parameters) {
  ArrayList<Integer> expected_types = new ArrayList<Integer>();
  for (int i=0; i<fn.parameters.size(); i++) {
    expected_types.add(fn.parameters.get(i).type);
  }

  int expected_count = expected_types.size();
  int parameter_count = parameters.size();

  if (expected_count != parameter_count) {
    SemErr("argument count error: function " +
           fn.name +
           " expects " +
           expected_count +
           " arguments, got " +
           parameter_count);
  }

  for (int i=0; i<expected_count; i++) {
    if (expected_types.get(i) != parameters.get(i)) {
      SemErr("type error: function " +
               fn.name +
               " parameter at position " +
               i +
               " should be " +
               TYPES_INVERSE.get(expected_types.get(i)) +
               " got " +
               TYPES_INVERSE.get(parameters.get(i)));
    }
  }
}

private void checkReturnType(SymbolTable.Function fn, int type) {
  if (fn.type == type) return;

  SemErr("type error: return type of function " +
         fn.name +
         " is " +
         TYPES_INVERSE.get(fn.type) +
         ", got " +
         TYPES_INVERSE.get(type) +
         " instead");
}

private void checkAssignmentType(int expected, int type) {
  if (expected == type) return;

  SemErr("incompatible types: trying to assign " +
         TYPES_INVERSE.get(type) +
         " value to " +
         TYPES_INVERSE.get(expected) +
         " variable");
}

/*----------------------------------------------------------------------------*
 * Functions to resolve LL(1) conflicts:                                      *
 * Used as IF ( ... ) in expressions.                                         *
 *--------------------------------------------------------------------------- */

private boolean isFunctionCall() {
  scanner.ResetPeek();
  return (la.kind == _identifier && scanner.Peek().kind == _lpar);
}

private boolean isLengthAccess() {
  scanner.ResetPeek();
  return (la.kind == _identifier && scanner.Peek().kind == _dot);
}

/*----------------------------------------------------------------------------*
 * Functions to keep the code cleaner                                         *
 *----------------------------------------------------------------------------*/
private SymbolTable.Function newFunction(String name, int type) {
  SymbolTable.Function fn = st.addFunction(name, type, gen.newLabel());
  gen.emit("LAB", fn.label);//gen.emit("LAB, fn.label);
  st.openScope(fn.type);
  // Address of the scope?
  return fn;
}

private void functionReturn() {
  if (st.top_scope.level == 1 && st.top_scope.return_type == UNDEFINED) return;
  gen.emit("RET");
}

private int safeParseInt(String val) {
  try {
    return Integer.parseInt(val);
  } catch (NumberFormatException e) {
    SemErr("too large integer");
    return Integer.MAX_VALUE;
  }
}

/*----------------------------------------------------------------------------*
 * Weird 'struct' for passing the IdAccess data.                              *
 * This might neither be a good practice in Java, nor a good way to pass the  *
 * IdAccess data. However this feels the most intuitive right now.            *
 *----------------------------------------------------------------------------*/
private class IdAccessData {
  public boolean local; // 'local' or 'dynamic'
  public SymbolTable.Variable var; // The variable in question
}


CHARACTERS
  cr  = '\r'.
  lf  = '\n'.
  tab = '\t'.

  zero          = '0'.
  nonZeroDigit  = "123456789".
  digit         = zero + nonZeroDigit .
  letter        = 'A' .. 'Z' + 'a' .. 'z' + '_' + '$'.

  char          = ANY - "'" - '\\' - cr - lf.
  stringChar    = ANY - "\"" - '\\' - cr - lf.

TOKENS
  identifier    = letter { letter | digit }.
  integer       = digit { digit }.

  colon   = ":".
  comma   = ",".
  dec     = "--".
  dot     = ".".
  inc     = "++".
  lbrace  = "{".
  lbrack  = "[".
  lpar    = "(".
  minus   = "-".
  not     = "!".
  plus    = "+".
  rbrace  = "}".
  rbrack  = "]".
  rpar    = ")".
  tilde   = "~".

COMMENTS FROM "/*" TO "*/" NESTED
COMMENTS FROM "//" TO lf

IGNORE cr + lf + tab

PRODUCTIONS

/*----------------------------------------------------------------------------*/

Compiler                               (. st.openScope(UNDEFINED); .)
= FuncDecl
  MainFuncDecl                         (.
                                          gen.emit("HLT");
                                          st.closeScope();
                                       .)
.

/*----------------------------------------------------------------------------*/

MainFuncDecl                           (.
                                          int return_type;
                                          st.openScope(UNDEFINED);
                                       .)
= "main"                               (. gen.emit("LAB", MAIN_LABEL); .)
  FuncBody<out return_type>
.

/*----------------------------------------------------------------------------*/

FuncDecl                                (. gen.emit("JMP", MAIN_LABEL); .)
=
{                                       (. int return_type; SymbolTable.Function fn; .)
  FuncHeader<out fn>
  FuncBody<out return_type>             (. checkReturnType(fn, return_type); .)
}
.

/*----------------------------------------------------------------------------*/

FuncHeader<out SymbolTable.Function fn> (. int type; .)
= Type<out type> identifier             (. fn = newFunction(t.val, type); .)
  '(' ParamList<fn> ')'                 // newFunction opens new scope
.

/*----------------------------------------------------------------------------*/

ParamList<SymbolTable.Function fn>      (. String name; int type; SymbolTable.Variable var; .)
=
[ Type<out type> Ident<out name>        (.
                                           var = st.addVariable(name, type);
                                           fn.addParameter(var);
                                        .)
{ ',' Type<out type> Ident<out name>    (.
                                           var = st.addVariable(name, type);
                                           fn.addParameter(var);
                                        .)
}
]
.

/*----------------------------------------------------------------------------*/

FuncBody<out int return_type>
= "begin"
  VarDecl
  StatementList
  ReturnStatement<out return_type>
  "end"                                 (. st.closeScope(); .)
.

/*----------------------------------------------------------------------------*/

VarDecl                                 (. String name; int type; .)
=
{ Type<out type>
  Ident<out name>                       (. st.addVariable(name, type); .)
  ';'
}                                       // TODO: check the frame size?
.

/*----------------------------------------------------------------------------*/

ReturnStatement<out int type>
= "return"
  Expr<out type>
  ";"                                    (. functionReturn(); .)
.

/*----------------------------------------------------------------------------*/

StatementList
= { Statement }
.

/*----------------------------------------------------------------------------*/

Statement
=                                         (. int type, if_end; .)
  "if" '(' Expr<out type> ')'             (.
                                             checkBoolean(type, "if statement");
                                             if_end = gen.newLabel();
                                             gen.emit("JZE", if_end);
                                          .)
  "then" Statement                        (. gen.emit("LAB", if_end); .)
|                                         (. int type, while_start; .)
  "do"                                    (.
                                             while_start = gen.newLabel();
                                             gen.emit("LAB", while_start);
                                          .)
  Statement
  "while" '('
  Expr<out type>                          (.
                                             checkBoolean(type, "while condition");
                                             // we don't have jump if command word,
                                             // so need to negate and use jump if not
                                             gen.emit("NOT");
                                             gen.emit("JZE", while_start);
                                          .)
  ')' ';'
|                                         (. int type; .)
  "print" '(' Expr<out type> ')' ';'      (. gen.emit("WRI"); .)
| "begin" StatementList "end"
|                                         (. int type, expected; String cw; .)
  IdAccess<out IdAccessData data>
  '='                                     //(. gen.emit("ENT", var.adr); .)
                                          // cannot assign to procedure
  Expr<out type>                          (.
                                             expected = data.local ? data.var.type : INTEGER;
                                             checkAssignmentType(expected, type);
                                             cw = data.local ? "STL" : "STM";
                                             gen.emit(cw);
                                          .)
  ';'    // Meaning: assignment
.

/*----------------------------------------------------------------------------*/

Expr<out int type>                        (. int t1; type = UNDEFINED; .)
= RelExpr<out t1>                         (. type = t1; .)
{                                         (.
                                             int t2;
                                             checkBoolean(t1, "&& operator");
                                             gen.emit("ENT", 1);
                                             gen.emit("REQ");
                                          .)
  "&&"
  RelExpr<out t2>                         (.
                                             checkBoolean(t2, "&& operator");
                                             gen.emit("ENT", 1);
                                             gen.emit("REQ");
                                             gen.emit("ADD");
                                             gen.emit("ENT", 2);
                                             gen.emit("REQ");
                                             type = BOOLEAN;
                                          .)
}
.

/*----------------------------------------------------------------------------*/

RelExpr<out int type>                     (. int t1; type = UNDEFINED; .)
= SimExpr<out t1>                         (. type = t1; .)
{                                         (. int t2; String op; .)
  RelOp<out op>                           (. checkInt(t1, "Relation operator (<, >)"); .)
  SimExpr<out t2>                         (.
                                             checkInt(t2, "Relation operator (<, >)");
                                             gen.emit(op);
                                             type = BOOLEAN;
                                          .)
}
.

/*----------------------------------------------------------------------------*/

SimExpr<out int type>                     (. type = UNDEFINED; .)
= '!'                                     (. int t; .)
  SimExpr<out t>                          (.
                                           checkBoolean(t, "! operator");
                                             gen.emit("NOT");
                                             type = BOOLEAN;
                                          .)
|                                         (. int t1; .)
  Term<out t1>                            (. type = t1; .)
{                                         (. int t2; String op; .)
  AddOp<out op>                           (. checkInt(t1, "Add (subtract) operation"); .)
  Term<out t2>                            (.
                                             checkInt(t2, "Add (subtract) operation");
                                             gen.emit(op);
                                             type = INTEGER;
                                          .)
}
.

/*----------------------------------------------------------------------------*/

Term<out int type>                        (. int t1; type = UNDEFINED; .)
= Factor<out t1>                          (. type = t1; .)
{                                         (. int t2; String op; .)
  MulOp<out op>                           (. checkInt(t1, "Multiplication (division) operation"); .)
  Factor<out t2>                          (.
                                             checkInt(t2, "Multiplication (division)");
                                             gen.emit(op);
                                             type = INTEGER;
                                          .)
}
.

/*----------------------------------------------------------------------------*/

Factor<out int type>                     (. type = UNDEFINED; .)
=
( IF (isFunctionCall()) FunctionCall<out type>
| IF (isLengthAccess())
                                         (. IdAccessData data; .)
  IdAccess<out data> '.' "length"        (.
                                            checkArrayType(data.var.type, "array length access");
                                            gen.emit("LDL");
                                            gen.emit("LDM");
                                            type = INTEGER;
                                         .)
| '(' Expr<out type> ')'
|                                        (. IdAccessData data; String cw; .)
  IdAccess<out data>                     (.
                                            if (data.local) {
                                              type = data.var.type;
                                              cw = "LDL";
                                            } else {
                                              type = INTEGER;
                                              cw = "LDM";
                                            }
                                            gen.emit(cw);
                                            //type = INTEGER;
                                         .)
|                                        (. int val; .)
  integer                                (.
                                            val = safeParseInt(t.val);
                                            gen.emit("ENT", val);
                                            type = INTEGER;
                                         .)
| "true"                                 (. gen.emit("ENT", 1); type = BOOLEAN; .)
| "false"                                (. gen.emit("ENT", 0); type = BOOLEAN; .)
| "new" "int" '[' Expr<out type> ']'     (.
                                            checkInt(type, "array allocation size");
                                            type = INTEGER_ARRAY;
                                            gen.emit("ALC");
                                            // Note that the array length can be negative at the moment

                                         .)
| "read" '(' ')'                         (. gen.emit("REA"); .)
| '-' Factor<out type>                   (.
                                            checkInt(type, "Negation operator");
                                            gen.emit("UMN");
                                         .)
)
.

/*----------------------------------------------------------------------------*/

AddOp<out String op>                     (. op = "ADD"; .)
= '+'                                    (. op = "ADD"; .)
| '-'                                    (. op = "SUB"; .)
.

/*----------------------------------------------------------------------------*/

MulOp<out String op>                (. op = "MUL"; .)
= '*'                                    (. op = "MUL"; .)
| '/'                                    (. op = "DIV"; .)
.

/*----------------------------------------------------------------------------*/

RelOp<out String op>
= '<'                                    (. op = "RLT"; .)
.

/*----------------------------------------------------------------------------*/

IdAccess<out IdAccessData data>          (.
                                            String name;
                                            SymbolTable.Variable var;
                                            data = new IdAccessData();
                                         .)
= Ident<out name>                        (.
                                            var = (SymbolTable.Variable)st.findSymbol(name);
                                            data.var = var;
                                            data.local = true;
                                            gen.emit("ENT", var.adr);
                                         .)
[                                        (. int type; .)
 '['                                     (.
                                            data.local = false;
                                            gen.emit("LDL");
                                         .)
  Expr<out type>                         (.
                                            gen.emit("ENT", 1);
                                            gen.emit("ADD");
                                            gen.emit("ADD");
                                         .)
  ']'
]
.

/*----------------------------------------------------------------------------*/

Ident<out String name>
= identifier                            (. name = t.val; .)
.

/*----------------------------------------------------------------------------*/

FunctionCall<out int type>              (.
                                           String name;
                                           ArrayList<Integer> parameters;
                                           SymbolTable.Function fn;
                                        .)
= Ident<out name>                       (.
                                           fn = (SymbolTable.Function)st.findSymbol(name);
                                           type = fn.type;
                                        .)
  '(' CallParams<out parameters> ')'    (.
                                           checkCallParameterTypes(fn, parameters);
                                           gen.emit("SFR", st.top_scope.frame_size);
                                           gen.emit("SBR", fn.label, parameters.size());
                                        .)
.

/*----------------------------------------------------------------------------*/

CallParams<out ArrayList<Integer>
           parameters>                  (.
                                           int type;
                                           parameters = new ArrayList<Integer>();
                                        .)
=
[ Expr<out type>                        (. parameters.add(type); .)
{
  ',' Expr<out type>                    (. parameters.add(type); .)
}
]
.

/*----------------------------------------------------------------------------*/

Type<out int type>                      (. type = UNDEFINED; .)
= "int"                                 (. type = INTEGER; .)
| "int[]"                               (. type = INTEGER_ARRAY; .)
| "boolean"                             (. type = BOOLEAN; .)
.

END Compiler.
